/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.web.servlet.mvc

import java.util.concurrent.Callable
import java.util.concurrent.CountDownLatch
import java.util.concurrent.Executors
import java.util.concurrent.Future

import org.junit.jupiter.api.Test

import static org.junit.jupiter.api.Assertions.*

class SynchronizerTokensHolderTests {

    @Test
    // GRAILS-9923
    void testSerializable() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        holder.generateToken 'url1'
        holder.generateToken 'url1'
        holder.generateToken 'url2'

        ByteArrayOutputStream baos = new ByteArrayOutputStream()
        ObjectOutputStream oos = new ObjectOutputStream(baos)
        oos.writeObject holder
        byte[] data = baos.toByteArray()

        ObjectInputStream ios = new ObjectInputStream(new ByteArrayInputStream(data))
        def deserialized = ios.readObject()
        assertTrue deserialized instanceof SynchronizerTokensHolder

        SynchronizerTokensHolder holder2 = deserialized
        assertEquals holder2.currentTokens, holder.currentTokens
        assertEquals 2, holder2.currentTokens.size()

        holder.generateToken 'url3'
        assertEquals 2, holder2.currentTokens.size()

        holder2.generateToken 'url3'
        assertEquals 3, holder2.currentTokens.size()
    }

    @Test
    void testGenerate() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        assert holder.empty

        String url1 = 'url1'
        assertNotNull holder.generateToken(url1)
        assertEquals 1, holder.currentTokens.size()
        assertEquals 1, holder.currentTokens[url1].size()

        assertNotNull holder.generateToken(url1)
        assertEquals 1, holder.currentTokens.size()
        assertEquals 2, holder.currentTokens[url1].size()

        String url2 = 'url2'
        assertNotNull holder.generateToken(url2)
        assertEquals 2, holder.currentTokens.size()
        assertEquals 2, holder.currentTokens[url1].size()
        assertEquals 1, holder.currentTokens[url2].size()
    }

    @Test
    void testGeneratedTokensAreBoundedByUrlCount() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()

        String firstUrl = 'url0'
        String firstToken = holder.generateToken(firstUrl)
        (1..SynchronizerTokensHolder.DEFAULT_MAX_TOKEN_URLS).each { Integer index ->
            holder.generateToken("url${index}".toString())
        }

        assertEquals SynchronizerTokensHolder.DEFAULT_MAX_TOKEN_URLS, holder.currentTokens.size()
        assertFalse holder.isValid(firstUrl, firstToken)
    }

    @Test
    void testExistingOversizedUrlMapIsTrimmedWhenGeneratingToken() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String firstUrl = 'url0'
        holder.currentTokens[firstUrl] = [UUID.randomUUID()] as LinkedHashSet<UUID>
        (1..(SynchronizerTokensHolder.DEFAULT_MAX_TOKEN_URLS + 5)).each { Integer index ->
            holder.currentTokens["url${index}".toString()] = [UUID.randomUUID()] as LinkedHashSet<UUID>
        }

        String newToken = holder.generateToken('new-url')

        assertEquals SynchronizerTokensHolder.DEFAULT_MAX_TOKEN_URLS, holder.currentTokens.size()
        assertFalse holder.currentTokens.containsKey(firstUrl)
        assertTrue holder.isValid('new-url', newToken)
    }

    @Test
    void testExistingOversizedUrlMapKeepsUrlReceivingNewToken() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String refreshedUrl = 'url1'
        holder.currentTokens['url0'] = [UUID.randomUUID()] as LinkedHashSet<UUID>
        holder.currentTokens[refreshedUrl] = [UUID.randomUUID()] as LinkedHashSet<UUID>
        (2..(SynchronizerTokensHolder.DEFAULT_MAX_TOKEN_URLS + 5)).each { Integer index ->
            holder.currentTokens["url${index}".toString()] = [UUID.randomUUID()] as LinkedHashSet<UUID>
        }

        String newToken = holder.generateToken(refreshedUrl)

        assertEquals SynchronizerTokensHolder.DEFAULT_MAX_TOKEN_URLS, holder.currentTokens.size()
        assertTrue holder.currentTokens.containsKey(refreshedUrl)
        assertTrue holder.isValid(refreshedUrl, newToken)
    }

    @Test
    void testGeneratedTokensAreBoundedPerUrl() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String url = 'url1'
        String firstToken = holder.generateToken(url)
        String lastToken = null
        (1..SynchronizerTokensHolder.DEFAULT_MAX_TOKENS_PER_URL).each {
            lastToken = holder.generateToken(url)
        }

        assertEquals SynchronizerTokensHolder.DEFAULT_MAX_TOKENS_PER_URL, holder.currentTokens[url].size()
        assertFalse holder.isValid(url, firstToken)
        assertTrue holder.isValid(url, lastToken)
    }

    @Test
    void testExistingOversizedTokenSetIsTrimmedWhenGeneratingToken() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String url = 'url1'
        UUID firstToken = UUID.randomUUID()
        holder.currentTokens[url] = [firstToken] as LinkedHashSet<UUID>
        (1..(SynchronizerTokensHolder.DEFAULT_MAX_TOKENS_PER_URL + 5)).each {
            holder.currentTokens[url].add(UUID.randomUUID())
        }

        String newToken = holder.generateToken(url)

        assertEquals SynchronizerTokensHolder.DEFAULT_MAX_TOKENS_PER_URL, holder.currentTokens[url].size()
        assertFalse holder.currentTokens[url].contains(firstToken)
        assertTrue holder.isValid(url, newToken)
    }

    @Test
    void testIsValid() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        assertTrue holder.empty

        String url = 'url1'

        String token = holder.generateToken(url)
        assertTrue holder.isValid(url, token)
        assertFalse holder.isValid(url, token + '!')
    }

    @Test
    void testValidTokenCanOnlyBeConsumedOnce() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String url = 'url1'
        String token = holder.generateToken(url)

        assertTrue holder.isValidAndResetToken(url, token)
        assertFalse holder.isValidAndResetToken(url, token)
        assertTrue holder.empty
    }

    @Test
    void testInvalidTokensAreNotConsumed() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String url = 'url1'
        String token = holder.generateToken(url)

        assertFalse holder.isValidAndResetToken(url, null)
        assertFalse holder.isValidAndResetToken(url, '')
        assertFalse holder.isValidAndResetToken(url, token + '!')
        assertTrue holder.isValidAndResetToken(url, token)
    }

    @Test
    void testConsumingOneTokenKeepsSiblingTokensForSameUrl() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String url = 'url1'
        String token1 = holder.generateToken(url)
        String token2 = holder.generateToken(url)

        assertTrue holder.isValidAndResetToken(url, token1)
        assertFalse holder.isValidAndResetToken(url, token1)
        assertTrue holder.isValidAndResetToken(url, token2)
        assertTrue holder.empty
    }

    @Test
    void testConcurrentTokenConsumeAllowsOneSuccess() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        String url = 'url1'
        String token = holder.generateToken(url)
        CountDownLatch start = new CountDownLatch(1)
        def executor = Executors.newFixedThreadPool(2)

        try {
            List<Future<Boolean>> results = (1..2).collect {
                executor.submit({ ->
                    start.await()
                    holder.isValidAndResetToken(url, token)
                } as Callable<Boolean>)
            }
            start.countDown()

            assertEquals 1, results.count { Future<Boolean> result -> result.get() }
            assertTrue holder.empty
        }
        finally {
            executor.shutdownNow()
        }
    }

    @Test
    void testResetTokens() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()
        assertTrue holder.empty

        String url1 = 'url1'
        String url2 = 'url2'

        assertNotNull holder.generateToken(url1)
        assertNotNull holder.generateToken(url2)
        assertEquals 2, holder.currentTokens.size()

        holder.resetToken url1
        assertEquals 1, holder.currentTokens.size()

        holder.resetToken url2
        assertEquals 0, holder.currentTokens.size()
    }

    @Test
    void testResetToken() {
        SynchronizerTokensHolder holder = new SynchronizerTokensHolder()

        String url1 = 'url1'
        String url2 = 'url2'

        String token1 = holder.generateToken(url1)
        String token2 = holder.generateToken(url1)
        String token3 = holder.generateToken(url1)
        String token4 = holder.generateToken(url2)
        assertEquals 2, holder.currentTokens.size()

        holder.resetToken url1, token1
        assertEquals 2, holder.currentTokens.size()

        holder.resetToken url1, token2
        assertEquals 2, holder.currentTokens.size()

        holder.resetToken url1, token3
        assertEquals 1, holder.currentTokens.size()

        holder.resetToken url1, token4
        assertEquals 1, holder.currentTokens.size()

        holder.resetToken url1, token4 + '!'
        assertEquals 1, holder.currentTokens.size()

        holder.resetToken url2, token4
        assertEquals 0, holder.currentTokens.size()
    }
}
