/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.forge.feature.reloading

import spock.lang.Unroll

import org.grails.forge.ApplicationContextSpec
import org.grails.forge.application.ApplicationType
import org.grails.forge.fixture.CommandOutputFixture
import org.grails.forge.options.DevelopmentReloading
import org.grails.forge.options.JdkVersion
import org.grails.forge.options.Options

class SpringBootDevToolsSpec extends ApplicationContextSpec implements CommandOutputFixture {

    void "test spring-boot-devtools feature"() {
        when:
        def output = generate(ApplicationType.WEB, new Options(DevelopmentReloading.DEVTOOLS), [])
        def build = output['build.gradle']

        then:
        build.contains('developmentOnly "org.springframework.boot:spring-boot-devtools"')
    }

    @Unroll
    void "test spring-boot-devtools dependency is present for #applicationType application type"(ApplicationType applicationType) {
        when:
        def output = generate(applicationType, new Options(DevelopmentReloading.DEVTOOLS), [])
        def build = output['build.gradle']

        then:
        build.contains('developmentOnly "org.springframework.boot:spring-boot-devtools"')

        where:
        applicationType << [ApplicationType.WEB, ApplicationType.REST_API]
    }

    void "test there can be only one of Reloading feature"() {
        when:
        def devtoolsOutput = generate(ApplicationType.WEB, new Options(DevelopmentReloading.DEVTOOLS), [])
        def jrebelOutput = generate(ApplicationType.WEB, new Options(DevelopmentReloading.JREBEL), [])

        then:
        devtoolsOutput['build.gradle'].contains('developmentOnly "org.springframework.boot:spring-boot-devtools"')
        !devtoolsOutput['build.gradle'].contains('org.zeroturnaround.gradle.jrebel')

        jrebelOutput['build.gradle'].contains('org.zeroturnaround.gradle.jrebel')
        !jrebelOutput['build.gradle'].contains('developmentOnly "org.springframework.boot:spring-boot-devtools"')
    }

    void "test spring-boot-devtools is not applied when grails-micronaut is selected"() {
        expect:
        // Micronaut features require JDK 25+ because micronaut-core's ScopedValues
        // references java.lang.ScopedValue.CallableOp (JEP 506, finalized in JDK 25).
        !('spring-boot-devtools' in getFeatures(['grails-micronaut'], new Options(DevelopmentReloading.DEVTOOLS, JdkVersion.JDK_25)))
    }
}
