/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package page

abstract class SearchPage extends LifecyclePage {

	static at = {
		title == "${typeName()} Search"
	}
	static content = {
		searchSummary {
			$('div#s2ui_content div:last-child div:last-child').last().text()
		}
		submitBtn {
			$('a', id: 'searchButton')
		}
	}

	boolean assertNoResults() {
		// the results area is re-rendered via AJAX after the search submits,
		// so retry until the DOM settles instead of reading it once
		waitFor { driver.pageSource.contains('No results') }
		assert !driver.pageSource.contains('Showing')
		true
	}

	boolean assertNotSearched() {
		driver.pageSource.with {
			assert contains('Search')
			assert !contains('No results')
			assert !contains('Showing')
		}
		true
	}

	boolean assertResults(int start, int end, int total) {
		// the results summary is re-rendered via AJAX after the search submits,
		// so retry until the DOM settles instead of reading it once
		waitFor {
			searchSummary == "Showing $start through $end out of $total."
		}
		true
	}

	<T extends SearchPage> T submit(Class<T> expectedPageType) {
		clickAndWaitForPage(submitBtn, expectedPageType)
	}
}
