/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.datastore.gorm

import groovy.transform.CompileDynamic
import groovy.transform.CompileStatic

import org.slf4j.Logger
import org.slf4j.LoggerFactory

import org.springframework.transaction.PlatformTransactionManager
import org.springframework.util.ClassUtils

import grails.gorm.MultiTenant
import org.grails.datastore.gorm.finders.CountByFinder
import org.grails.datastore.gorm.finders.FindAllByBooleanFinder
import org.grails.datastore.gorm.finders.FindAllByFinder
import org.grails.datastore.gorm.finders.FindByBooleanFinder
import org.grails.datastore.gorm.finders.FindByFinder
import org.grails.datastore.gorm.finders.FinderMethod
import org.grails.datastore.gorm.finders.FindOrCreateByFinder
import org.grails.datastore.gorm.finders.FindOrSaveByFinder
import org.grails.datastore.gorm.finders.ListOrderByFinder
import org.grails.datastore.mapping.core.Datastore
import org.grails.datastore.mapping.core.connections.ConnectionSource
import org.grails.datastore.mapping.core.connections.ConnectionSourceSettings
import org.grails.datastore.mapping.core.connections.ConnectionSourcesSupport
import org.grails.datastore.mapping.model.PersistentEntity
import org.grails.datastore.mapping.reflect.MetaClassUtils

/**
 * Enhances a class with GORM methods
 *
 * @author Graeme Rocher
 */
@CompileStatic
class GormEnhancer implements Closeable {

    private static final Logger log = LoggerFactory.getLogger(GormEnhancer)
    private static final GormEnhancerRegistry STATE_REGISTRY = GormEnhancerRegistry.getInstance()

    private final GormRegistry registry
    private final List<String> connectionSourceNames
    final Datastore datastore
    boolean failOnError = false
    boolean markDirty = true

    /**
     * Whether to include external entities
     */
    boolean includeExternal = true

    /**
     * Whether to enhance classes dynamically using meta programming as well, only necessary for Java classes
     */
    final boolean dynamicEnhance

    /**
     * Retained for API compatibility. The registry-based enhancer no longer uses the
     * transaction manager directly; obtain it from the GORM APIs instead.
     */
    @Deprecated
    final PlatformTransactionManager transactionManager

    GormEnhancer(Datastore datastore) {
        this(datastore, null)
    }

    GormEnhancer(Datastore datastore, PlatformTransactionManager transactionManager, boolean failOnError = false, boolean dynamicEnhance = false, boolean markDirty = true) {
        this(datastore, transactionManager, new ConnectionSourceSettings().failOnError(failOnError).markDirty(markDirty))
    }

    /**
     * Construct a new GormEnhancer for the given arguments.
     *
     * @param datastore The datastore (required)
     * @param transactionManager Retained for constructor compatibility
     * @param settings The connection source settings (required)
     * @param registry The GORM registry (optional, defaults to singleton instance)
     */
    GormEnhancer(Datastore datastore,
                 PlatformTransactionManager transactionManager,
                 ConnectionSourceSettings settings,
                 GormRegistry registry = GormRegistry.getInstance()) {
        assert datastore != null, 'Datastore is required'
        assert settings != null, 'ConnectionSourceSettings is required'

        this.datastore = datastore
        this.registry = registry
        this.transactionManager = transactionManager
        this.dynamicEnhance = false

        this.failOnError = settings.isFailOnError()
        Boolean markDirty = settings.getMarkDirty()
        this.markDirty = markDirty == null ? true : markDirty

        this.connectionSourceNames = ConnectionSourceNameResolver.resolveConnectionSourceNames(datastore)

        String qualifier = ConnectionSourceNameResolver.resolveDefaultConnectionSourceName(datastore)
        registry.initializeDatastore(datastore, qualifier)

        registerApiFactories()
        registerConstraints(datastore)

        for (entity in datastore.mappingContext.persistentEntities) {
            registerEntity(entity)
        }
    }

    /**
     * Registers the {@link GormApiFactory} implementations this enhancer's datastore needs, before any
     * entity is registered.
     *
     * Adapters that build specialised API objects (for example Mongo's {@code MongoStaticApi}, which
     * {@code MongoEntity} casts to) override this. It is deliberately separate from
     * {@link #registerConstraints(Datastore)}: factory registration has nothing to do with constraints,
     * and hanging it off the constraints hook would break the moment a subclass reordered or skipped
     * that step.
     */
    protected void registerApiFactories() {
        // no-op by default: the registry falls back to DefaultGormApiFactory
    }

    /**
     * Registers a new entity with the GORM enhancer
     *
     * @param entity The entity
     */
    void registerEntity(PersistentEntity entity) {
        if (!entity.isExternal()) {
            // Delegate entity registration orchestration to the registry
            registry.registerEntity(entity, this)
        }
    }

    /**
     * Enhances all persistent entities.
     *
     * @param onlyExtendedMethods If only to add additional methods provides by subclasses of the GORM APIs
     */
    void enhance(boolean onlyExtendedMethods = false) {
        if (dynamicEnhance) {
            for (PersistentEntity e in datastore.mappingContext.persistentEntities) {
                if (e.external && !includeExternal) continue
                enhance(e, onlyExtendedMethods)
            }
        }
    }

    /**
     * Enhance and individual entity
     *
     * @param e The entity
     * @param onlyExtendedMethods If only to add additional methods provides by subclasses of the GORM APIs
     */
    void enhance(PersistentEntity e, boolean onlyExtendedMethods = false) {
        registerEntity(e)

        if (!(GroovyObject.isAssignableFrom(e.javaClass)) || dynamicEnhance) {
            addInstanceMethods(e)
            addStaticMethods(e)
        }
    }

    /**
     * Obtain all of the qualifiers (typically the connection names) for the datastore and entity
     *
     * @param datastore The datastore
     * @param entity The entity
     * @return The qualifiers
     */
    @CompileDynamic
    List<String> allQualifiers(Datastore datastore, PersistentEntity entity) {
        List<String> qualifiers = new ArrayList<>()
        qualifiers.addAll(ConnectionSourcesSupport.getConnectionSourceNames(entity))

        boolean isMultiTenant = MultiTenant.isAssignableFrom(entity.javaClass)
        boolean hasExplicitAll = qualifiers.contains(ConnectionSource.ALL)
        boolean hasExplicitNonDefaultDatasource = isMultiTenant &&
                !hasExplicitAll &&
                qualifiers.size() > 0 &&
                !qualifiers.equals(ConnectionSourcesSupport.DEFAULT_CONNECTION_SOURCE_NAMES)

        if ((isMultiTenant || hasExplicitAll) && !hasExplicitNonDefaultDatasource) {
            qualifiers.clear()
            if (datastore == this.datastore) {
                // Resolve the datastore's connection sources freshly rather than from the cached
                // list captured at construction: schema/database tenants added at runtime via
                // addTenantForSchema re-register entities through this enhancer, and those new
                // connections must be picked up here or the tenant's datastore is never mapped.
                qualifiers.addAll(ConnectionSourceNameResolver.resolveConnectionSourceNames(datastore))
            } else {
                def className = entity.name
                for (String q in connectionSourceNames) {
                    if (registry.getDatastore(className, q) == datastore) {
                        qualifiers.add(q)
                    }
                }

                if (qualifiers.isEmpty()) {
                    for (String q in registry.datastoresByQualifier.keySet()) {
                        if (registry.datastoresByQualifier.get(q) == datastore) {
                            qualifiers.add(q)
                        }
                    }
                }
            }
        }

        if (qualifiers.isEmpty()) {
            qualifiers.add(ConnectionSource.DEFAULT)
        }
        return qualifiers.unique()
    }

    List<String> getConnectionSourceNames() {
        return connectionSourceNames
    }

    /**
     * @return The GORM registry instance
     */
    static GormRegistry getRegistry() {
        return GormRegistry.instance
    }

    static PersistentEntity findEntity(Class entity) {
        GormApiResolver resolver = GormRegistry.instance.apiResolver
        Datastore ds = resolver.findDatastore(entity, null)
        return ds?.mappingContext?.getPersistentEntity(entity.name)
    }

    /**
     * Closes the enhancer clearing any stored static state
     */
    @CompileStatic
    void close() throws IOException {
        if (STATE_REGISTRY.getPreferredDatastore() == datastore) {
            STATE_REGISTRY.clearPreferredDatastore()
        }
        registry.removeDatastore(datastore)
        def metaClassRegistry = GroovySystem.metaClassRegistry
        for (entity in datastore.mappingContext.persistentEntities) {
            def cls = entity.javaClass
            def className = cls.name
            registry.removeEntityDatastore(className, datastore)
            
            boolean stillManaged = (registry.getStaticApi(className) != null)
            
            if (!stillManaged) {
                metaClassRegistry.removeMetaClass(cls)
            }
        }
    }

    @CompileDynamic
    protected void registerConstraints(Datastore datastore) {
        try {
            String className = 'org.grails.datastore.gorm.support.ConstraintRegistrar'
            ClassLoader classLoader = getClass().getClassLoader()
            if (ClassUtils.isPresent(className, classLoader)) {
                classLoader.loadClass(className).newInstance(datastore)
            }
        } catch (Throwable e) {
            log.debug("Unable to register GORM constraints. Not running a Grails environment. This can be safely ignored if you are not running Grails: $e.message", e)
        }
    }

    @CompileDynamic
    protected void addStaticMethods(PersistentEntity e) {
        def cls = e.javaClass
        ExpandoMetaClass mc = MetaClassUtils.getExpandoMetaClass(cls)

        mc.static.methodMissing = { String name, args ->
            def api = registry.findStaticApi(cls, null)
            try {
                return api.invokeMethod(name, args)
            } catch (MissingMethodException mme) {
                if (mme.method == name && mme.type == api.class) {
                    return api.methodMissing(name, args)
                }
                throw mme
            }
        }
        mc.static.propertyMissing = { String name ->
            def api = registry.findStaticApi(cls, null)
            try {
                return api.getProperty(name)
            } catch (MissingPropertyException mpe) {
                throw mpe
            } catch (Throwable t) {
                throw new MissingPropertyException(name, cls)
            }
        }
    }

    @CompileDynamic
    protected void addInstanceMethods(PersistentEntity e) {
        Class cls = e.javaClass
        ExpandoMetaClass mc = MetaClassUtils.getExpandoMetaClass(cls)

        if (mc.getMetaMethod('methodMissing', [String, Object] as Class[]) != null) {
            // See addStaticMethods: defer to a handler someone else installed rather than clobbering it.
            log.debug('Not installing GORM instance missing-method dispatch on {}: a handler is already installed', cls.name)
            return
        }

        mc.methodMissing = { String name, args ->
            def api = registry.findInstanceApi(cls, null)
            try {
                return api.invokeMethod(name, args)
            } catch (MissingMethodException mme) {
                if (mme.method == name && mme.type == api.class) {
                    return api.methodMissing(delegate, name, args)
                }
                throw mme
            }
        }
        mc.propertyMissing = { String name ->
            def api = registry.findInstanceApi(cls, null)
            try {
                return api.getProperty(name)
            } catch (MissingPropertyException mpe) {
                if (mpe.property == name && mpe.type == api.class) {
                    return api.propertyMissing(delegate, name)
                }
                throw mpe
            }
        }
        mc.propertyMissing = { String name, val ->
            registry.findInstanceApi(cls, null).setProperty(name, val)
        }
    }

    @Deprecated
    @CompileStatic
    static <D> GormStaticApi<D> findStaticApi(Class<D> entity) {
        GormRegistry.findStaticApi(entity)
    }

    @Deprecated
    @CompileStatic
    static <D> GormStaticApi<D> findStaticApi(Class<D> entity, String qualifier) {
        GormRegistry.findStaticApi(entity, qualifier)
    }

    @Deprecated
    @CompileStatic
    static <D> GormInstanceApi<D> findInstanceApi(Class<D> entity) {
        GormRegistry.findInstanceApi(entity)
    }

    @Deprecated
    @CompileStatic
    static <D> GormInstanceApi<D> findInstanceApi(Class<D> entity, String qualifier) {
        GormRegistry.findInstanceApi(entity, qualifier)
    }

    @Deprecated
    @CompileStatic
    static <D> GormValidationApi<D> findValidationApi(Class<D> entity) {
        GormRegistry.findValidationApi(entity)
    }

    @Deprecated
    @CompileStatic
    static <D> GormValidationApi<D> findValidationApi(Class<D> entity, String qualifier) {
        GormRegistry.findValidationApi(entity, qualifier)
    }

    @Deprecated
    @CompileStatic
    static Datastore findDatastore(Class entity) {
        GormRegistry.findDatastore(entity)
    }

    @Deprecated
    @CompileStatic
    static Datastore findDatastore(Class entity, String qualifier) {
        GormRegistry.findDatastore(entity, qualifier)
    }

    @Deprecated
    @CompileStatic
    protected <D> GormStaticApi<D> getStaticApi(Class<D> cls, String qualifier = ConnectionSource.DEFAULT) {
        GormRegistry.findStaticApi(cls, qualifier) as GormStaticApi<D>
    }

    @Deprecated
    @CompileStatic
    protected <D> GormInstanceApi<D> getInstanceApi(Class<D> cls, String qualifier = ConnectionSource.DEFAULT) {
        GormRegistry.findInstanceApi(cls, qualifier) as GormInstanceApi<D>
    }

    @Deprecated
    @CompileStatic
    protected <D> GormValidationApi<D> getValidationApi(Class<D> cls, String qualifier = ConnectionSource.DEFAULT) {
        GormRegistry.findValidationApi(cls, qualifier) as GormValidationApi<D>
    }

    @Deprecated
    @CompileStatic
    protected List<FinderMethod> createDynamicFinders() {
        createDynamicFinders(datastore)
    }

    @Deprecated
    @CompileStatic
    protected List<FinderMethod> createDynamicFinders(Datastore targetDatastore) {
        [new FindOrCreateByFinder(targetDatastore),
         new FindOrSaveByFinder(targetDatastore),
         new FindByFinder(targetDatastore),
         new FindAllByFinder(targetDatastore),
         new FindAllByBooleanFinder(targetDatastore),
         new FindByBooleanFinder(targetDatastore),
         new CountByFinder(targetDatastore),
         new ListOrderByFinder(targetDatastore)] as List<FinderMethod>
    }

}
