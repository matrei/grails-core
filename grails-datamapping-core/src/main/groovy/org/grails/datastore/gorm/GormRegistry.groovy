/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  'License'); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  'AS IS' BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.datastore.gorm

import java.util.concurrent.ConcurrentHashMap

import groovy.transform.CompileDynamic
import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j

import org.springframework.transaction.PlatformTransactionManager
import org.springframework.util.ClassUtils

import grails.gorm.MultiTenant
import grails.gorm.multitenancy.CurrentTenantHolder
import org.grails.datastore.gorm.finders.FinderMethod
import org.grails.datastore.mapping.core.Datastore
import org.grails.datastore.mapping.core.connections.ConnectionSource
import org.grails.datastore.mapping.core.connections.ConnectionSourcesSupport
import org.grails.datastore.mapping.core.connections.MultipleConnectionSourceCapableDatastore
import org.grails.datastore.mapping.core.connections.SingleSessionCapableDatastore
import org.grails.datastore.mapping.model.MappingContext
import org.grails.datastore.mapping.model.PersistentEntity
import org.grails.datastore.mapping.multitenancy.MultiTenantCapableDatastore
import org.grails.datastore.mapping.multitenancy.MultiTenancySettings
import org.grails.datastore.mapping.multitenancy.exceptions.TenantNotFoundException
import org.grails.datastore.mapping.reflect.NameUtils
import org.grails.datastore.mapping.transactions.TransactionCapableDatastore

/**
 * A registry of GORM API objects. This registry is used to decouple the API
 * objects from the static state in GormEnhancer.
 *
 * It implements an O(M+N) memory strategy where:
 * M = Number of Entities
 * N = Number of Connections (Tenants)
 *
 * @author Walter Duque de Estrada
 * @since 8.0.0
 */
@Slf4j
@CompileStatic
class GormRegistry {

    private static final GormRegistry instance = new GormRegistry()

    /**
     * The {@link #withConnectionScope} blocks running on this thread, innermost first; {@code null} outside every one.
     */
    private static final ThreadLocal<Deque<ConnectionScope>> CONNECTION_SCOPES = new ThreadLocal<>()
    private final GormApiFactory defaultApiFactory = new DefaultGormApiFactory()
    final GormApiResolver apiResolver = new GormApiResolver(this)
    final GormStaticApiRegistry staticApiRegistry = new GormStaticApiRegistry(this)
    final GormInstanceApiRegistry instanceApiRegistry = new GormInstanceApiRegistry(this)
    final GormValidationApiRegistry validationApiRegistry = new GormValidationApiRegistry(this)

    final Map<String, Datastore> datastoresByQualifier = new ConcurrentHashMap<>()
    private final Map<String, Map<String, Datastore>> entityDatastores = new ConcurrentHashMap<>()
    // Records each entity's declared (non-DEFAULT) connection order, so a later removal that
    // strips the DEFAULT routing can be repaired deterministically rather than falling back to
    // entityDatastores' unspecified ConcurrentHashMap iteration order.
    private final Map<String, List<String>> entityConnectionOrder = new ConcurrentHashMap<>()
    private final Map<Class, String> normalizedEntityKeysByClass = new ConcurrentHashMap<>()
    private final Map<String, String> normalizedEntityKeysByName = new ConcurrentHashMap<>()
    private final Map<String, String> normalizedQualifiers = new ConcurrentHashMap<>()
    final Map<Class, Datastore> datastoresByType = new ConcurrentHashMap<>()
    private final Map<Class, GormApiFactory> apiFactoriesByDatastoreType = new ConcurrentHashMap<>()
    final Set<Datastore> allDatastores = Collections.newSetFromMap(new ConcurrentHashMap<Datastore, Boolean>())

    static GormRegistry getInstance() {
        return instance
    }

    /**
     * @return The default datastore
     */
    Datastore getDefaultDatastore() {
        return datastoresByQualifier.get(ConnectionSource.DEFAULT)
    }

    /**
     * Resets the registry.
     * Nominally unused in core mapping runtime code, but heavily used by testing frameworks to reset state between spec executions.
     */
    static void reset() {
        instance.resetInstance()
    }

    private void resetInstance() {
        staticApiRegistry.clear()
        instanceApiRegistry.clear()
        validationApiRegistry.clear()
        datastoresByQualifier.clear()
        entityDatastores.clear()
        entityConnectionOrder.clear()
        normalizedEntityKeysByClass.clear()
        normalizedEntityKeysByName.clear()
        normalizedQualifiers.clear()
        datastoresByType.clear()
        apiFactoriesByDatastoreType.clear()
        allDatastores.clear()
        GormEnhancerRegistry.getInstance().clearPreferredDatastore()
        GormEnhancerRegistry.getInstance().clearResolvingDatastoreDepth()
    }

    static <D> GormStaticApi<D> findStaticApi(Class<D> entity) {
        instance.resolveStaticApi(entity, (String) null)
    }

    static <D> GormStaticApi<D> findStaticApi(Class<D> entity, String qualifier) {
        instance.resolveStaticApi(entity, qualifier)
    }

    static <D> GormInstanceApi<D> findInstanceApi(Class<D> entity) {
        instance.resolveInstanceApi(entity, (String) null)
    }

    static <D> GormInstanceApi<D> findInstanceApi(Class<D> entity, String qualifier) {
        instance.resolveInstanceApi(entity, qualifier)
    }

    static <D> GormValidationApi<D> findValidationApi(Class<D> entity) {
        instance.resolveValidationApi(entity, (String) null)
    }

    static <D> GormValidationApi<D> findValidationApi(Class<D> entity, String qualifier) {
        instance.resolveValidationApi(entity, qualifier)
    }

    static Datastore findDatastore(Class entity) {
        instance.apiResolver.findDatastore(entity, (String) null)
    }

    static Datastore findDatastore(Class entity, String qualifier) {
        instance.apiResolver.findDatastore(entity, qualifier)
    }

    /**
     * Registers a custom GormApiFactory for a specific datastore type.
     * Nominally unused within the core mapping module, but invoked dynamically by external datastore implementations (e.g. Hibernate, MongoDB) to customize API generation.
     */
    void registerApiFactory(Class datastoreType, GormApiFactory factory) {
        apiFactoriesByDatastoreType.put(datastoreType, factory)
    }

    GormApiFactory getApiFactory(Datastore datastore) {
        GormApiFactory factory = apiFactoriesByDatastoreType.get(datastore.getClass())
        if (factory == null) {
            for (Map.Entry<Class, GormApiFactory> entry in apiFactoriesByDatastoreType.entrySet()) {
                if (entry.key.isInstance(datastore)) {
                    return entry.value
                }
            }
            return defaultApiFactory
        }
        return factory
    }

    /**
     * Finds a single transaction manager if only one datastore is registered.
     * Nominally unused, but invoked at compile-time by transactional AST transformations.
     */
    PlatformTransactionManager findSingleTransactionManager() {
        return findSingleTransactionManager(ConnectionSource.DEFAULT)
    }

    /**
     * Finds a single transaction manager for a specific qualifier.
     * Nominally unused, but invoked at compile-time by transactional AST transformations.
     */
    PlatformTransactionManager findSingleTransactionManager(String qualifier) {
        Datastore ds = getDatastoreByString((String) null, qualifier)
        if (ds == null) {
            if (defaultDatastore == null) {
                throw new IllegalStateException('No GORM implementations configured. Ensure GORM has been initialized correctly')
            }
            return null
        }
        if (ds instanceof TransactionCapableDatastore) {
            return ((TransactionCapableDatastore) ds).transactionManager
        }
        return null
    }

    /**
     * Finds a transaction manager for a specific entity class and qualifier.
     * Nominally unused, but invoked at compile-time by transactional/service AST transformations.
     */
    PlatformTransactionManager findTransactionManager(Class entityClass, String qualifier) {
        Datastore ds = getDatastore(entityClass, qualifier)
        if (ds == null) {
            // The qualifier may be a tenant ID rather than a registered datastore qualifier
            // (e.g. DISCRIMINATOR / SCHEMA multi-tenancy). Fall back via the full resolver
            // which understands the multi-tenancy mode and returns the correct datastore.
            ds = apiResolver.findDatastore(entityClass, qualifier)
        }
        if (ds == null) {
            if (defaultDatastore == null) {
                throw new IllegalStateException('No GORM implementations configured. Ensure GORM has been initialized correctly')
            }
            return null
        }
        if (ds instanceof TransactionCapableDatastore) {
            return ((TransactionCapableDatastore) ds).transactionManager
        }
        return null
    }

    /**
     * Finds a transaction manager for a specific entity class.
     * Nominally unused, but invoked at compile-time by transactional/service AST transformations.
     */
    PlatformTransactionManager findTransactionManager(Class entityClass) {
        return findTransactionManager(entityClass, ConnectionSource.DEFAULT)
    }

    /**
     * Finds a datastore for a specific qualifier (connection name).
     */
    Datastore getDatastore(String qualifier) {
        return getDatastoreByString((String) null, qualifier)
    }

    /**
     * Internal method to avoid redundant normalization.
     */
    Datastore getDatastoreDirect(String normalizedClassName, String normalizedQualifier) {
        if (normalizedClassName != null) {
            Map<String, Datastore> mappedDatastores = entityDatastores.get(normalizedClassName)
            if (mappedDatastores != null) {
                Datastore ds = mappedDatastores.get(normalizedQualifier)
                if (ds != null) {
                    return ds
                }
                // No fallback to "whichever datastore this entity happens to be mapped to". The
                // entity's unqualified (DEFAULT) route is decided deterministically at registration
                // time (see registerEntityDatastores/repairDefaultRouting) and stored under the
                // DEFAULT key; picking an arbitrary map entry here would make an entity mapped only
                // to non-default connections read/write a different database between runs.
                Datastore qualifierDs = datastoresByQualifier.get(normalizedQualifier)
                if (qualifierDs != null && qualifierDs.getMappingContext()?.getPersistentEntity(normalizedClassName) != null) {
                    return qualifierDs
                }
                return null
            }
        }

        Datastore ds = datastoresByQualifier.get(normalizedQualifier)
        if (ds == null && ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
            if (allDatastores.size() == 1) {
                return allDatastores.iterator().next()
            }
        }
        return ds
    }

    /**
     * Internal method to avoid ambiguity.
     */
    Datastore getDatastoreByString(String className, String qualifier) {
        return getDatastoreDirect(className != null ? normalizeEntityKey(className) : null, normalizeQualifier(qualifier))
    }

    /**
     * Finds a datastore for a specific entity class.
     * Part of the public API for external integrations and manual datastore lookup.
     */
    Datastore getDatastore(Class entityClass) {
        return getDatastore(entityClass, ConnectionSource.DEFAULT)
    }

    /**
     * Finds a datastore for a specific entity class and qualifier.
     */
    Datastore getDatastore(Class entityClass, String qualifier) {
        return getDatastoreByString(entityClass != null ? normalizeEntityKey(entityClass) : (String) null, qualifier)
    }

    /**
     * Finds a datastore for an entity class name and qualifier.
     */
    Datastore getDatastore(String className, String qualifier) {
        return getDatastoreByString(className, qualifier)
    }

    /**
     * Registers GORM APIs for an entity.
     */
    void registerApi(String className, GormStaticApi staticApi, GormInstanceApi instanceApi, GormValidationApi validationApi) {
        String normalizedClassName = normalizeEntityKey(className)
        staticApiRegistry.register(normalizedClassName, staticApi)
        instanceApiRegistry.register(normalizedClassName, instanceApi)
        validationApiRegistry.register(normalizedClassName, validationApi)
    }

    /**
     * Registers a datastore for a qualifier. (O(N) part)
     */
    void registerDatastore(String qualifier, Datastore datastore) {
        if (datastore == null) return
        String normalizedQualifier = normalizeQualifier(qualifier)
        datastoresByQualifier.put(normalizedQualifier, datastore)
        allDatastores.add(datastore)
    }

    /**
     * Initializes a datastore, registering its type and default qualifier.
     */
    void initializeDatastore(Datastore datastore) {
        if (datastore == null) return
        registerDatastore(ConnectionSource.DEFAULT, datastore)
        datastoresByType.put(datastore.getClass(), datastore)
    }

    /**
     * Registers a datastore.
     */
    void registerDatastore(Datastore datastore) {
        initializeDatastore(datastore)
    }

    /**
     * Registers a datastore by its type.
     * Nominally unused in core mapping runtime code, but used by test suites and external integrations.
     */
    void registerDatastoreByType(Datastore datastore) {
        if (datastore == null) return
        datastoresByType.put(datastore.getClass(), datastore)
        allDatastores.add(datastore)
    }

    /**
     * Registers a datastore by qualifier only, without adding it to the global type-based discovery.
     */
    void registerDatastoreByQualifier(String qualifier, Datastore datastore) {
        if (qualifier != null && datastore != null) {
            datastoresByQualifier.put(normalizeQualifier(qualifier), datastore)
        }
    }

    /**
     * Removes a datastore from discovery by its class type.
     * Nominally unused in core mapping runtime code, but used by testing frameworks to clean up dynamic datastores.
     */
    void removeDatastoreByType(Class datastoreType) {
        if (datastoreType == null) return
        datastoresByType.remove(datastoreType)
    }

    /**
     * Removes a datastore from discovery by its instance type.
     * Nominally unused in core mapping runtime code, but used by testing frameworks to clean up dynamic datastores.
     */
    void removeDatastoreByType(Datastore datastore) {
        if (datastore == null) return
        removeDatastoreByType(datastore.getClass())
    }

    /**
     * Removes a datastore from global discovery (allDatastores and datastoresByType)
     * but keeps it in datastoresByQualifier.
     * Nominally unused in core mapping runtime code, but used by test suites to verify multi-datastore isolation.
     */
    void removeDatastoreFromDiscovery(Datastore datastore) {
        if (datastore == null) return
        allDatastores.remove(datastore)
        datastoresByType.remove(datastore.getClass())
    }

    /**
     * Completely removes a datastore from the registry.
     */
    void removeDatastore(Datastore datastore) {
        if (datastore == null) return
        removeConstraints()
        allDatastores.remove(datastore)
        datastoresByType.remove(datastore.getClass())

        Iterator<Map.Entry<String, Datastore>> it = datastoresByQualifier.entrySet().iterator()
        while (it.hasNext()) {
            if (it.next().value == datastore) it.remove()
        }

        for (Map.Entry<String, Map<String, Datastore>> entry in entityDatastores.entrySet()) {
            Map<String, Datastore> entityMap = entry.value
            Iterator<Map.Entry<String, Datastore>> eit = entityMap.entrySet().iterator()
            while (eit.hasNext()) {
                if (eit.next().value == datastore) eit.remove()
            }
            repairDefaultRouting(entry.key, entityMap)
        }

        staticApiRegistry.removeDatastore(datastore)
        instanceApiRegistry.removeDatastore(datastore)
        validationApiRegistry.removeDatastore(datastore)

        // When the last datastore goes away (application shutdown, or test/dev-reload cycles),
        // drop the normalization caches too: normalizedEntityKeysByClass holds strong Class
        // references (classloader retention across reloads) and normalizedQualifiers grows by one
        // entry per tenant identifier ever seen.
        if (allDatastores.isEmpty()) {
            normalizedEntityKeysByClass.clear()
            normalizedEntityKeysByName.clear()
            normalizedQualifiers.clear()
        }
    }

    /**
     * Removes a datastore for a specific entity.
     */
    void removeEntityDatastore(String className, Datastore datastore) {
        if (className != null && datastore != null) {
            Map<String, Datastore> entityMap = entityDatastores.get(className)
            if (entityMap != null) {
                Iterator<Map.Entry<String, Datastore>> eit = entityMap.entrySet().iterator()
                while (eit.hasNext()) {
                    if (eit.next().value == datastore) eit.remove()
                }
                repairDefaultRouting(className, entityMap)
            }
        }
    }

    /**
     * If removing a datastore stripped this entity's DEFAULT routing but left other declared
     * connections behind, re-point DEFAULT deterministically at the earliest-declared connection
     * that remains (per {@link #registerEntityDatastores}), rather than leaving unqualified
     * lookups to fall back on entityDatastores' unspecified ConcurrentHashMap iteration order.
     */
    private void repairDefaultRouting(String className, Map<String, Datastore> mappedDatastores) {
        if (mappedDatastores.isEmpty() || mappedDatastores.containsKey(ConnectionSource.DEFAULT)) {
            return
        }
        List<String> declaredOrder = entityConnectionOrder.get(className)
        if (declaredOrder == null) {
            return
        }
        for (String qualifier in declaredOrder) {
            Datastore candidate = mappedDatastores.get(qualifier)
            if (candidate != null) {
                mappedDatastores.put(ConnectionSource.DEFAULT, candidate)
                return
            }
        }
    }

    /**
     * Checks if a specific datastore is explicitly registered for an entity.
     */
    boolean isDatastoreRegisteredForEntity(String className, Datastore datastore) {
        if (className != null && datastore != null) {
            Map<String, Datastore> entityMap = entityDatastores.get(normalizeEntityKey(className))
            if (entityMap != null && entityMap.values().contains(datastore)) {
                return true
            }
        }
        return false
    }

    GormStaticApi getStaticApi(Class entityClass) {
        return staticApiRegistry.get(normalizeEntityKey(entityClass))
    }

    GormInstanceApi getInstanceApi(Class entityClass) {
        return instanceApiRegistry.get(normalizeEntityKey(entityClass))
    }

    GormValidationApi getValidationApi(Class entityClass) {
        return validationApiRegistry.get(normalizeEntityKey(entityClass))
    }

    GormStaticApi getStaticApi(Class entityClass, String qualifier) {
        return staticApiRegistry.get(normalizeEntityKey(entityClass), normalizeQualifier(qualifier))
    }

    GormInstanceApi getInstanceApi(Class entityClass, String qualifier) {
        return instanceApiRegistry.get(normalizeEntityKey(entityClass), normalizeQualifier(qualifier))
    }

    GormValidationApi getValidationApi(Class entityClass, String qualifier) {
        return validationApiRegistry.get(normalizeEntityKey(entityClass), normalizeQualifier(qualifier))
    }

    /**
     * Runs the callable with the unqualified operations on the entity routed to the named connection: an explicit
     * static call such as {@code Book.list()}, an instance call such as {@code book.save()}, and a query built by
     * either. This is what a {@code withConnection} block promises; the closure's delegate only covers the calls
     * that do not name the class. Such a lookup resolves exactly as if it had named the connection, so for a
     * multi-tenant entity the block's connection takes precedence over the current tenant, as an explicitly
     * named connection does. An operation that names its own connection, and every other entity, is unaffected.
     * The scope belongs to the calling thread. Blocks nest, and the previous connection applies again when an
     * inner one ends. A block for the default connection undoes any enclosing one for its duration, and outside
     * every block it costs nothing.
     *
     * @param entity The entity whose operations follow the connection
     * @param connectionName The connection
     * @param callable What to run
     * @return What the callable returns
     */
    static <T> T withConnectionScope(Class entity, String connectionName, Closure<T> callable) {
        return runInScope(instance.normalizeEntityKey(entity), instance.normalizeQualifier(connectionName), callable)
    }

    /**
     * Runs the callable with the unqualified operations on every entity mapped to the named connection routed to
     * it, as {@link #withConnectionScope(Class, String, Closure)} does for one entity. An entity that is not mapped
     * to the connection keeps its own. This is what a transaction opened for a connection, as a method annotated
     * {@code @Transactional(connection = 'books')} opens one, promises for the calls made inside it.
     *
     * @param connectionName The connection
     * @param callable What to run
     * @return What the callable returns
     */
    static <T> T withConnectionScope(String connectionName, Closure<T> callable) {
        return runInScope(null, instance.normalizeQualifier(connectionName), callable)
    }

    private static <T> T runInScope(String entityKey, String qualifier, Closure<T> callable) {
        Deque<ConnectionScope> scopes = CONNECTION_SCOPES.get()
        if (ConnectionSource.DEFAULT == qualifier && scopes == null) {
            // Nothing to undo: outside every block the operations are on their default connections already.
            return callable.call()
        }
        if (scopes == null) {
            scopes = new ArrayDeque<ConnectionScope>()
            CONNECTION_SCOPES.set(scopes)
        }
        scopes.push(new ConnectionScope(entityKey, qualifier))
        try {
            return callable.call()
        }
        finally {
            scopes.pop()
            if (scopes.isEmpty()) {
                CONNECTION_SCOPES.remove()
            }
        }
    }

    /**
     * @return whether a {@link #withConnectionScope} block is running on this thread, for any entity
     */
    static boolean insideConnectionScope() {
        Deque<ConnectionScope> scopes = CONNECTION_SCOPES.get()
        return scopes != null && !scopes.isEmpty()
    }

    /**
     * @return the connection that the innermost {@link #withConnectionScope} block applying to the entity routes
     * its unqualified operations to, or {@code null} when none is running for it on this thread
     */
    private static String scopedConnection(String normalizedClassName) {
        Deque<ConnectionScope> scopes = CONNECTION_SCOPES.get()
        if (scopes == null) {
            return null
        }
        for (ConnectionScope scope : scopes) {
            boolean applies = scope.entityKey == null ?
                    instance.isMappedToConnection(normalizedClassName, scope.qualifier) :
                    scope.entityKey == normalizedClassName
            if (applies) {
                return scope.qualifier
            }
        }
        return null
    }

    /**
     * Whether a block for every entity on the connection covers this one. A block for the default connection undoes
     * an enclosing one for every entity; any other covers only the entities mapped to its connection, so that none
     * is sent to a connection it has no mapping for.
     *
     * <p>A multi-tenant entity is never covered: which tenant its operations belong to is what the tenant context
     * says, and a transaction opened for a connection, or for a tenant's own schema or database, does not answer
     * that. Naming the connection on the entity still routes it, as it always did.
     */
    private boolean isMappedToConnection(String normalizedClassName, String qualifier) {
        if (ConnectionSource.DEFAULT == qualifier) {
            return true
        }
        Map<String, Datastore> mapped = entityDatastores.get(normalizedClassName)
        if (mapped == null || !mapped.containsKey(qualifier)) {
            return false
        }
        Datastore defaultDatastore = getDatastoreByString(normalizedClassName, ConnectionSource.DEFAULT)
        PersistentEntity entity = defaultDatastore?.mappingContext?.getPersistentEntity(normalizedClassName)
        return entity == null || !entity.isMultiTenant()
    }

    /**
     * One {@link #withConnectionScope} block: the entity it routes, by normalized name, or {@code null} for every
     * entity mapped to its connection.
     */
    private static final class ConnectionScope {

        final String entityKey

        final String qualifier

        ConnectionScope(String entityKey, String qualifier) {
            this.entityKey = entityKey
            this.qualifier = qualifier
        }
    }

    GormStaticApi resolveStaticApi(Class entityClass) {
        return resolveStaticApi(entityClass, (String) null)
    }

    GormStaticApi resolveStaticApi(Class entityClass, String qualifier) {
        String normalizedClassName = normalizeEntityKey(entityClass)
        // A lookup that names no connection follows the scope exactly as if it had named that connection.
        String normalizedQualifier = normalizeQualifier(qualifier != null ? qualifier : scopedConnection(normalizedClassName))

        if (MultiTenant.isAssignableFrom(entityClass)) {
            // Priority 1: Explicit qualifier that doesn't match default is likely a tenant ID
            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                GormStaticApi api = staticApiRegistry.getDirect(normalizedClassName, normalizedQualifier)
                if (api != null) return api
            }

            // Priority 2: Check current bound tenant if using default qualifier
            Datastore ds = getDatastoreDirect(normalizedClassName, normalizedQualifier)
            if (ds instanceof MultiTenantCapableDatastore) {
                MultiTenantCapableDatastore mtds = (MultiTenantCapableDatastore) ds
                MultiTenancySettings.MultiTenancyMode mode = mtds.getMultiTenancyMode()
                boolean strictMode = mode == MultiTenancySettings.MultiTenancyMode.DATABASE ||
                        mode == MultiTenancySettings.MultiTenancyMode.SCHEMA
                Serializable tenantId = CurrentTenantHolder.get(mtds)
                if (tenantId == null && strictMode) {
                    try {
                        tenantId = mtds.tenantResolver.resolveTenantIdentifier()
                    } catch (TenantNotFoundException e) {
                        throw e
                    }
                }
                if (tenantId != null && !ConnectionSource.DEFAULT.equals(tenantId.toString())) {
                    GormStaticApi api = staticApiRegistry.getDirect(normalizedClassName, tenantId.toString())
                    if (api != null) return api
                }
            }

            // Priority 3: Fall back to default API instance if specialized one not found,
            // but keep the qualifier so the API can handle tenant binding
            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                GormStaticApi api = staticApiRegistry.getDirect(normalizedClassName, ConnectionSource.DEFAULT)
                if (api != null) return api
            }
        }

        return staticApiRegistry.getDirect(normalizedClassName, normalizedQualifier)
    }

    GormInstanceApi resolveInstanceApi(Class entityClass) {
        return resolveInstanceApi(entityClass, (String) null)
    }

    GormInstanceApi resolveInstanceApi(Class entityClass, String qualifier) {
        String normalizedClassName = normalizeEntityKey(entityClass)
        String normalizedQualifier = normalizeQualifier(qualifier != null ? qualifier : scopedConnection(normalizedClassName))

        if (MultiTenant.isAssignableFrom(entityClass)) {
            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                GormInstanceApi api = instanceApiRegistry.getDirect(normalizedClassName, normalizedQualifier)
                if (api != null) return api
            }

            Datastore ds = getDatastoreDirect(normalizedClassName, normalizedQualifier)
            if (ds instanceof MultiTenantCapableDatastore) {
                MultiTenantCapableDatastore mtds = (MultiTenantCapableDatastore) ds
                MultiTenancySettings.MultiTenancyMode mode = mtds.getMultiTenancyMode()
                boolean strictMode = mode == MultiTenancySettings.MultiTenancyMode.DATABASE ||
                        mode == MultiTenancySettings.MultiTenancyMode.SCHEMA
                Serializable tenantId = CurrentTenantHolder.get(mtds)
                if (tenantId == null && strictMode) {
                    try {
                        tenantId = mtds.tenantResolver.resolveTenantIdentifier()
                    } catch (TenantNotFoundException e) {
                        throw e
                    }
                }
                if (tenantId != null && !ConnectionSource.DEFAULT.equals(tenantId.toString())) {
                    GormInstanceApi api = instanceApiRegistry.getDirect(normalizedClassName, tenantId.toString())
                    if (api != null) return api
                }
            }

            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                GormInstanceApi api = instanceApiRegistry.getDirect(normalizedClassName, ConnectionSource.DEFAULT)
                if (api != null) return api
            }
        }

        return instanceApiRegistry.getDirect(normalizedClassName, normalizedQualifier)
    }

    /**
     * Resolves the validation API for the given entity class.
     * Nominally unused in core mapping runtime code, but invoked at compile-time by GORM's AST transformations.
     */
    GormValidationApi resolveValidationApi(Class entityClass) {
        return resolveValidationApi(entityClass, (String) null)
    }

    /**
     * Resolves the validation API for the given entity class and qualifier.
     * Nominally unused in core mapping runtime code, but invoked at compile-time by GORM's AST transformations.
     */
    GormValidationApi resolveValidationApi(Class entityClass, String qualifier) {
        String normalizedClassName = normalizeEntityKey(entityClass)
        String normalizedQualifier = normalizeQualifier(qualifier != null ? qualifier : scopedConnection(normalizedClassName))

        if (MultiTenant.isAssignableFrom(entityClass)) {
            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                GormValidationApi api = validationApiRegistry.getDirect(normalizedClassName, normalizedQualifier)
                if (api != null) return api
            }

            Datastore ds = getDatastoreDirect(normalizedClassName, normalizedQualifier)
            if (ds instanceof MultiTenantCapableDatastore) {
                MultiTenantCapableDatastore mtds = (MultiTenantCapableDatastore) ds
                MultiTenancySettings.MultiTenancyMode mode = mtds.getMultiTenancyMode()
                boolean strictMode = mode == MultiTenancySettings.MultiTenancyMode.DATABASE ||
                        mode == MultiTenancySettings.MultiTenancyMode.SCHEMA
                Serializable tenantId = CurrentTenantHolder.get(mtds)
                if (tenantId == null && strictMode) {
                    try {
                        tenantId = mtds.tenantResolver.resolveTenantIdentifier()
                    } catch (TenantNotFoundException e) {
                        throw e
                    }
                }
                if (tenantId != null && !ConnectionSource.DEFAULT.equals(tenantId.toString())) {
                    GormValidationApi api = validationApiRegistry.getDirect(normalizedClassName, tenantId.toString())
                    if (api != null) return api
                }
            }

            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                GormValidationApi api = validationApiRegistry.getDirect(normalizedClassName, ConnectionSource.DEFAULT)
                if (api != null) return api
            }
        }

        return validationApiRegistry.getDirect(normalizedClassName, normalizedQualifier)
    }

    GormStaticApi getStaticApi(String className) {
        return staticApiRegistry.get(normalizeEntityKey(className))
    }

    GormStaticApi getStaticApi(String className, String qualifier) {
        return staticApiRegistry.get(normalizeEntityKey(className), normalizeQualifier(qualifier))
    }

    GormInstanceApi getInstanceApi(String className) {
        return instanceApiRegistry.get(normalizeEntityKey(className))
    }

    GormInstanceApi getInstanceApi(String className, String qualifier) {
        return instanceApiRegistry.get(normalizeEntityKey(className), normalizeQualifier(qualifier))
    }

    GormValidationApi getValidationApi(String className) {
        return validationApiRegistry.get(normalizeEntityKey(className))
    }

    GormValidationApi getValidationApi(String className, String qualifier) {
        return validationApiRegistry.get(normalizeEntityKey(className), normalizeQualifier(qualifier))
    }

    private Map<String, Datastore> getInternalMap(Map<String, Map<String, Datastore>> rootMap, String key) {
        Map<String, Datastore> map = rootMap.get(key)
        if (map == null) {
            map = new ConcurrentHashMap<String, Datastore>()
            Map<String, Datastore> prior = rootMap.putIfAbsent(key, map)
            if (prior != null) {
                return prior
            }
        }
        return map
    }

    String normalizeEntityKey(Object entityKey) {
        if (entityKey == null) {
            return null
        }
        if (entityKey instanceof Class) {
            Class entityClass = (Class) entityKey
            String existing = normalizedEntityKeysByClass.get(entityClass)
            if (existing != null) {
                return existing
            }
            String computed = NameUtils.getClassName(entityClass)
            String normalized = normalizeEntityKey(computed)
            if (normalized == null) {
                return null
            }
            String prior = normalizedEntityKeysByClass.putIfAbsent(entityClass, normalized)
            return prior != null ? prior : normalized
        } else {
            String className = entityKey.toString()
            String existing = normalizedEntityKeysByName.get(className)
            if (existing != null) {
                return existing
            }
            String normalized = className.trim()
            if (normalized.isEmpty()) {
                return null
            }
            String prior = normalizedEntityKeysByName.putIfAbsent(className, normalized)
            return prior != null ? prior : normalized
        }
    }

    /**
     * @deprecated Use {@code normalizeEntityKey(Class)}.
     */
    @Deprecated
    String normalizeEntityKeyFromClass(Class entityClass) {
        normalizeEntityKey(entityClass)
    }

    String normalizeQualifier(String qualifier) {
        if (qualifier == null) {
            return ConnectionSource.DEFAULT
        }
        String existing = normalizedQualifiers.get(qualifier)
        if (existing != null) {
            return existing
        }
        String normalized = qualifier.trim()
        if (normalized.isEmpty() || ConnectionSource.OLD_DEFAULT.equalsIgnoreCase(normalized)) {
            normalized = ConnectionSource.DEFAULT
        }
        String prior = normalizedQualifiers.putIfAbsent(qualifier, normalized)
        return prior != null ? prior : normalized
    }

    /**
     * @deprecated Use {@code normalizeQualifier(String)}.
     */
    @Deprecated
    String normalizeQualifierByString(String qualifier) {
        normalizeQualifier(qualifier)
    }

    /**
     * Register API objects for a persistent entity.
     * Creates and registers StaticApi, InstanceApi, and ValidationApi for the given entity.
     * Part of the public API for external plugins and test environments.
     *
     * @param className The entity class name
     * @param staticApi The static API implementation
     * @param instanceApi The instance API implementation
     * @param validationApi The validation API implementation
     */
    void registerEntityApis(String className, GormStaticApi staticApi, GormInstanceApi instanceApi, GormValidationApi validationApi) {
        registerApi(className, staticApi, instanceApi, validationApi)
    }

    /**
     * Register datastores for a persistent entity across multiple connection sources.
     * Handles entity-specific datastore mappings for multi-tenant and multi-datasource scenarios.
     *
     * @param className The entity class name
     * @param datastore The datastore to register
     * @param connectionSourceNames The connection source names to register the datastore for
     * @param entity The persistent entity (for entity-specific qualifier resolution)
     */
    void registerEntityDatastores(String className, Object datastore, List<String> connectionSourceNames, Object entity) {

        if (datastore == null) return
        String normalizedClassName = normalizeEntityKey(className)
        if (normalizedClassName == null) {
            return
        }

        Datastore defaultDatastore = (Datastore) datastore
        List<String> qualifiers = connectionSourceNames ?: Collections.singletonList(ConnectionSource.DEFAULT)
        boolean multiTenantEntity = entity instanceof PersistentEntity && ((PersistentEntity) entity).isMultiTenant()

        // Accumulate the entity's qualifier routing into a local map and publish it with a single
        // put below: remove-then-repopulate would open a window where a concurrent lookup sees no
        // routing for this entity at all and falls back to the wrong datastore.
        Map<String, Datastore> newEntityDatastores = new ConcurrentHashMap<>()
        List<String> declaredOrder = new ArrayList<>()

        Datastore primaryDatastore = defaultDatastore

        // Register datastores for each connection source. For each qualifier, attempt to resolve a
        // connection-specific child datastore. If the resolution falls back to the parent (meaning
        // the qualifier is a runtime tenant ID, not a datasource connection name), skip registration
        // for non-DEFAULT qualifiers in multi-tenant mode so that we do not overwrite the correctly
        // registered child datastores (e.g. those added by addTenantForSchemaInternal).
        for (String connectionSourceName in qualifiers) {
            String normalizedQualifier = normalizeQualifier(connectionSourceName)
            Datastore qualifierDatastore = defaultDatastore
            if (defaultDatastore instanceof MultipleConnectionSourceCapableDatastore &&
                    !ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                try {
                    Datastore resolved = ((MultipleConnectionSourceCapableDatastore) defaultDatastore)
                            .getDatastoreForConnection(normalizedQualifier)
                    if (resolved != null) {
                        qualifierDatastore = resolved
                    }
                } catch (Throwable e) {
                    // qualifier is not a datasource connection name; keep defaultDatastore
                    log.debug('Ignoring failure resolving connection {} for entity {}: {}', normalizedQualifier, className, e.message)
                }
            }
            // Skip non-DEFAULT qualifiers that resolve back to the parent for multi-tenant entities.
            // Those qualifiers are runtime tenant IDs handled at the session level, not datasource names.
            if (multiTenantEntity && !ConnectionSource.DEFAULT.equals(normalizedQualifier) &&
                    qualifierDatastore == defaultDatastore) {
                continue
            }
            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier) && primaryDatastore == defaultDatastore) {
                primaryDatastore = qualifierDatastore
            }
            registerDatastoreByQualifier(normalizedQualifier, qualifierDatastore)
            newEntityDatastores.put(normalizedQualifier, qualifierDatastore)
            if (!ConnectionSource.DEFAULT.equals(normalizedQualifier)) {
                declaredOrder.add(normalizedQualifier)
            }
        }

        // If the entity does not explicitly include DEFAULT, route its unqualified (no-connection)
        // operations. Real datastores manage a session per connection, so DEFAULT goes to the first
        // explicit connection's datastore. A single-session datastore (the in-memory mock used by
        // unit tests) keeps unqualified operations on the parent it manages — its connection
        // children exist only for explicit access and have no harness-flushed session.
        if (!qualifiers.collect { String it -> normalizeQualifier(it) }.contains(ConnectionSource.DEFAULT)) {
            Datastore defaultConnectionDatastore = primaryDatastore
            if (defaultDatastore instanceof SingleSessionCapableDatastore) {
                defaultConnectionDatastore = defaultDatastore
            }
            if (defaultConnectionDatastore != null) {
                newEntityDatastores.put(ConnectionSource.DEFAULT, defaultConnectionDatastore)
            }
        }

        if (newEntityDatastores.isEmpty()) {
            entityDatastores.remove(normalizedClassName)
            entityConnectionOrder.remove(normalizedClassName)
        }
        else {
            entityDatastores.put(normalizedClassName, newEntityDatastores)
            entityConnectionOrder.put(normalizedClassName, Collections.unmodifiableList(declaredOrder))
        }
    }

    /**
     * Registers an entity-specific datastore override.
     */
    void registerEntityDatastore(String className, String qualifier, Datastore datastore) {
        if (datastore != null) {
            String normalizedClassName = normalizeEntityKey(className)
            if (normalizedClassName == null) {
                return
            }
            String normalizedQualifier = normalizeQualifier(qualifier)
            getInternalMap(entityDatastores, normalizedClassName).put(normalizedQualifier, datastore)
        }
    }

    /**
     * Creates dynamic finders for the default datastore
     *
     * @return List of finder methods
     */
    List<FinderMethod> createDynamicFinders(Datastore targetDatastore) {
        createDynamicFinders(new DatastoreResolver() {
            @Override
            Datastore resolve() {
                targetDatastore
            }
        }, targetDatastore.getMappingContext())
    }

    /**
     * Creates dynamic finders using the given resolver and mapping context.
     *
     * @param resolver The datastore resolver
     * @param mappingContext The mapping context
     * @return List of finder methods
     */
    List<FinderMethod> createDynamicFinders(DatastoreResolver resolver, MappingContext mappingContext) {
        Datastore ds = resolver.resolve()
        if (ds != null) {
            return getApiFactory(ds).createDynamicFinders(resolver, mappingContext)
        }
        return []
    }

    /**
     * Create a DatastoreResolver for a class and optional qualifier.
     */
    DatastoreResolver createClassDatastoreResolver(Class cls, String qualifier = ConnectionSource.DEFAULT) {
        String normalizedQualifier = normalizeQualifier(qualifier)
        return new DatastoreResolver() {
            @Override
            Datastore resolve() {
                apiResolver.findDatastore(cls, normalizedQualifier)
            }
        }
    }

    /**
     * Create a GormStaticApi instance.
     * Uses a bound resolver (always returns the given datastore) at registration time so that
     * tenant-resolving DatastoreResolvers are not invoked before any tenant context is active.
     */
    GormStaticApi createStaticApi(Class cls, Datastore datastore, String qualifier) {
        DatastoreResolver boundResolver = { datastore } as DatastoreResolver
        return getApiFactory(datastore).createStaticApi(cls, datastore.mappingContext, boundResolver, qualifier, this)
    }

    /**
     * Create a GormInstanceApi instance.
     * Uses a bound resolver (always returns the given datastore) at registration time so that
     * tenant-resolving DatastoreResolvers are not invoked before any tenant context is active.
     */
    GormInstanceApi createInstanceApi(Class cls, Datastore datastore, boolean failOnError, boolean markDirty) {
        DatastoreResolver boundResolver = { datastore } as DatastoreResolver
        return getApiFactory(datastore).createInstanceApi(cls, datastore.mappingContext, boundResolver, this, failOnError, markDirty)
    }

    /**
     * Create a GormValidationApi instance.
     * Uses a bound resolver (always returns the given datastore) at registration time so that
     * tenant-resolving DatastoreResolvers are not invoked before any tenant context is active.
     */
    GormValidationApi createValidationApi(Class cls, Datastore datastore) {
        DatastoreResolver boundResolver = { datastore } as DatastoreResolver
        return getApiFactory(datastore).createValidationApi(cls, datastore.mappingContext, boundResolver, this)
    }

    /**
     * Register API objects for a persistent entity.
     * Part of the public API for external plugins and test environments.
     */
    void registerEntityApis(Class cls, GormStaticApi staticApi, GormInstanceApi instanceApi, GormValidationApi validationApi) {
        registerEntityApis(cls.name, staticApi, instanceApi, validationApi)
    }

    /**
     * De-registers the old Grails 2 'unique' constraint, if present.
     *
     * Mirrors {@link GormEnhancer#registerConstraints(Datastore)}'s reflective, soft-dependency
     * lookup - a no-op outside a Grails 2 environment. Not scoped to a specific datastore (the
     * underlying constraint registry this clears is global), so takes no parameters, same as the
     * original. Lives here, not on {@code GormEnhancer}, because {@link #removeDatastore(Datastore)}
     * already owns every other piece of teardown for a datastore going away (API deregistration,
     * datastore mapping cleanup, metaclass cleanup); constraint removal is one more.
     */
    @CompileDynamic
    void removeConstraints() {
        try {
            String className = 'org.apache.groovy.grails.validation.ConstrainedProperty'
            ClassLoader classLoader = getClass().getClassLoader()
            if (ClassUtils.isPresent(className, classLoader)) {
                classLoader.loadClass(className).removeConstraint('unique')
            }
        } catch (Throwable e) {
            log.debug('Not running in Grails 2 environment, cannot de-register constraints. This exception can be safely ignored if you are not using Grails 2. {}', e.message, e)
        }
    }

    /**
     * Initialize a datastore with GORM.
     * Orchestrates datastore registration. Constraint registration is handled by
     * {@link GormEnhancer#registerConstraints(Datastore)}; entity-specific registration is still
     * handled by GormEnhancer.
     *
     * @param datastore The datastore to initialize
     * @param defaultQualifier The default connection source qualifier
     */
    void initializeDatastore(Object datastore, String defaultQualifier) {
        if (datastore == null) return

        // Register datastore with default qualifier
        Datastore typedDatastore = (Datastore) datastore
        registerDatastore(defaultQualifier, typedDatastore)
        datastoresByType.put(typedDatastore.getClass(), typedDatastore)
    }

    /**
     * Register a persistent entity with GORM, orchestrating API and datastore registration.
     * This delegates to a GormEnhancer for creating the API instances.
     *
     * @param entity The persistent entity to register
     * @param enhancer The GormEnhancer that provides API creation
     */
    void registerEntity(PersistentEntity persistentEntity, GormEnhancer enhancer) {
        if (!persistentEntity) {
            throw new IllegalArgumentException('Argument [persistentEntity] cannot be null')
        }
        if (!enhancer) {
            throw new IllegalArgumentException('Argument [enhancer] cannot be null')
        }

        String className = persistentEntity.name

        // Always (re)register API singletons so classloader or datastore changes do not leave stale API instances.
        final Class cls = persistentEntity.javaClass
        Datastore datastore = enhancer.datastore

        GormStaticApi staticApi = createStaticApi(cls, datastore, ConnectionSource.DEFAULT)
        GormInstanceApi instanceApi = createInstanceApi(cls, datastore, enhancer.failOnError, enhancer.markDirty)
        GormValidationApi validationApi = createValidationApi(cls, datastore)

        registerEntityApis(className, staticApi, instanceApi, validationApi)

        // Register datastore mappings
        Datastore datastoreForMappings = enhancer.datastore
        List<String> qualifiers = enhancer.allQualifiers(datastore, persistentEntity)
        registerEntityDatastores(className, datastoreForMappings, qualifiers, persistentEntity)

        // Eagerly allocate APIs ONLY for the datasources the entity is EXPLICITLY mapped to. This is
        // the bounded "M" side of the O(M+N) strategy: an entity's declared datasources are a small,
        // known, finite set, so materializing them up front is cheap and avoids first-access latency.
        //
        // Runtime tenant qualifiers and the ALL wildcard's connections are the unbounded "N" side
        // (potentially many tenants, discovered at runtime) and MUST stay lazily allocated on first
        // access: eager-allocating them would reintroduce O(M×N) memory and cannot handle tenants
        // added at runtime (e.g. addTenantForSchema). The DEFAULT connection is already allocated above.
        for (String mapped in ConnectionSourcesSupport.getConnectionSourceNames(persistentEntity)) {
            String mappedQualifier = normalizeQualifier(mapped)
            if (!ConnectionSource.DEFAULT.equals(mappedQualifier) && !ConnectionSource.ALL.equals(mappedQualifier)) {
                getStaticApi(cls, mappedQualifier)
                getInstanceApi(cls, mappedQualifier)
                getValidationApi(cls, mappedQualifier)
            }
        }
    }

}
