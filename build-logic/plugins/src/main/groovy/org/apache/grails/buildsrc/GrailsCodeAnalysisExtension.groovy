/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.apache.grails.buildsrc

import javax.inject.Inject

import groovy.transform.CompileStatic

import org.gradle.api.Project
import org.gradle.api.file.DirectoryProperty
import org.gradle.api.model.ObjectFactory

@CompileStatic
abstract class GrailsCodeAnalysisExtension {

    /**
     * Defaults to rootProject.layout.buildDirectory/code-analysis/pmd.
     * Directory for PMD configuration files (e.g. pmd.xml).
     */
    final DirectoryProperty pmdDirectory

    /**
     * Defaults to rootProject.layout.buildDirectory/reports/code-analysis.
     * PMD and SpotBugs XML reports will be written here.
     */
    final DirectoryProperty reportsDirectory

    private final Project project

    @Inject
    GrailsCodeAnalysisExtension(ObjectFactory objects, Project project) {
        this.project = project
        pmdDirectory = objects.directoryProperty().convention(
                project.rootProject.layout.buildDirectory.dir('code-analysis/pmd')
        )
        reportsDirectory = objects.directoryProperty().convention(
                project.rootProject.layout.buildDirectory.dir('reports/code-analysis')
        )
    }

    /**
     * Opts this project into PMD and configures it immediately, so the build script can customize the
     * {@code pmd*} tasks directly afterwards. {@code -Pgrails.code-analysis.enabled.pmd=false} keeps PMD off.
     */
    void enablePmd() {
        GrailsCodeAnalysisPlugin.enablePmd(project, this)
    }

    /**
     * Opts this project into SpotBugs and configures it immediately, so the build script can customize the
     * {@code spotbugs*} tasks directly afterwards. {@code -Pgrails.code-analysis.enabled.spotbugs=false} keeps
     * SpotBugs off.
     */
    void enableSpotbugs() {
        GrailsCodeAnalysisPlugin.enableSpotbugs(project, this)
    }
}
