/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package grails.testing.web.interceptor

import java.lang.reflect.ParameterizedType

import groovy.transform.CompileDynamic
import groovy.transform.CompileStatic

import org.springframework.web.servlet.ModelAndView

import grails.artefact.Interceptor
import grails.testing.web.GrailsWebUnitTest
import grails.util.GrailsNameUtils
import grails.web.mapping.UrlMappingInfo
import org.grails.plugins.web.interceptors.GrailsInterceptorHandlerInterceptorAdapter
import org.grails.plugins.web.interceptors.InterceptorArtefactHandler
import org.grails.testing.ParameterizedGrailsUnitTest
import org.grails.web.mapping.ForwardUrlMappingInfo
import org.grails.web.mapping.mvc.UrlMappingsHandlerMapping
import org.grails.web.util.GrailsApplicationAttributes

@CompileStatic
trait InterceptorUnitTest<T> implements ParameterizedGrailsUnitTest<T>, GrailsWebUnitTest {

    private boolean hasBeenMocked = false

    /**
     * Mock the interceptor for the given name
     *
     * @param interceptorClass The interceptor class
     * @return The mocked interceptor
     */
    @CompileDynamic
    <I extends Interceptor> I mockInterceptor(Class<I> interceptorClass) {
        def artefact = grailsApplication.addArtefact(InterceptorArtefactHandler.TYPE, interceptorClass)
        defineBeans {
            "${artefact.propertyName}"(artefact.clazz) { bean ->
                bean.autowire = true
            }
        }
        getHandlerInterceptor()
                .setInterceptors(applicationContext.getBeansOfType(Interceptor).values() as Interceptor[])
        applicationContext.getBean(artefact.propertyName, interceptorClass)
    }

    /**
     * Execute the given request with the registered interceptors
     *
     * @param arguments The arguments
     * @param callable A callable containing an invocation of a controller action
     * @return The result of the callable execution, or {@code null} if an interceptor stops the request or an exception is thrown
     */
    Object withInterceptors(Map<String, Object> arguments, Closure callable) {
        ensureInterceptorHasBeenMocked()
        def urlMappingInfo = withRequest(arguments)
        def handlerInterceptor = getHandlerInterceptor()
        try {
            if (handlerInterceptor.preHandle(request, response, this)) {
                def result = callable.call()
                ModelAndView modelAndView = null
                def modelAndViewObject = request.getAttribute(GrailsApplicationAttributes.MODEL_AND_VIEW)
                if (modelAndViewObject instanceof ModelAndView) {
                    modelAndView = (ModelAndView) modelAndViewObject
                }
                else if (result instanceof Map) {
                    modelAndView =  new ModelAndView(urlMappingInfo?.actionName ?: 'index', new HashMap<String, Object>((Map) result))
                }
                else if (result instanceof ModelAndView) {
                    return (ModelAndView) result
                }
                handlerInterceptor.postHandle(request, response, this, modelAndView)
                return result
            }
        } catch (Exception e) {
            handlerInterceptor.afterCompletion(request, response, this, e)
        }
        null
    }

    /**
     * Allows testing of the interceptor directly by setting up an incoming request that can be matched prior to invoking the
     * interceptor
     *
     * @param arguments Named arguments specifying the controller/action or URI that interceptor should match
     *
     * @return The {@link UrlMappingInfo} object
     */
    @CompileDynamic
    UrlMappingInfo withRequest(Map<String, Object> arguments) {
        ensureInterceptorHasBeenMocked()
        UrlMappingInfo urlMappingInfo = null
        if (arguments.uri) {
            request.requestURI = arguments.uri.toString()
        } else {
            urlMappingInfo = new ForwardUrlMappingInfo(arguments)
            request.setAttribute(UrlMappingsHandlerMapping.MATCHED_REQUEST, urlMappingInfo)
        }

        for (String name : request.attributeNames.findAll { String candidate -> candidate.endsWith(InterceptorArtefactHandler.MATCH_SUFFIX) }) {
            request.removeAttribute(name)
        }
        urlMappingInfo
    }

    private GrailsInterceptorHandlerInterceptorAdapter getHandlerInterceptor() {
        applicationContext.getBean(GrailsInterceptorHandlerInterceptorAdapter)
    }

    void mockArtefact(Class<?> interceptorClass) {
        mockInterceptor((Class<? extends Interceptor>) interceptorClass)
    }

    String getBeanName(Class<?> interceptorClass) {
        GrailsNameUtils.getPropertyName(interceptorClass)
    }

    private Class<T> getInterceptorTypeUnderTest() {
        def parameterizedType = getClass().genericInterfaces.find { genericInterface ->
            genericInterface instanceof ParameterizedType &&
                    InterceptorUnitTest.isAssignableFrom((Class)((ParameterizedType)genericInterface).rawType)
        } as ParameterizedType
        parameterizedType?.actualTypeArguments[0] as Class<T>
    }

    T getInterceptor() {
        ensureInterceptorHasBeenMocked()
        getArtefactInstance()
    }

    private void ensureInterceptorHasBeenMocked() {
        if (!hasBeenMocked) {
            mockInterceptor(getInterceptorTypeUnderTest() as Class<Interceptor>)
            hasBeenMocked = true
        }
    }
}
