/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

package org.grails.gorm.graphql.fetcher.impl

import graphql.schema.DataFetchingEnvironment
import groovy.transform.CompileStatic
import groovy.transform.InheritConstructors
import org.grails.datastore.gorm.GormEntity
import org.grails.gorm.graphql.fetcher.DefaultGormDataFetcher
import org.grails.gorm.graphql.fetcher.DeletingGormDataFetcher
import org.grails.gorm.graphql.response.delete.GraphQLDeleteResponseHandler

/**
 * A class for deleting entities with GraphQL
 *
 * @param <T> The domain returnType to delete
 * @author James Kleeh
 * @since 1.0.0
 */
@CompileStatic
@InheritConstructors
class DeleteEntityDataFetcher<T> extends DefaultGormDataFetcher<T> implements DeletingGormDataFetcher<T> {

    GraphQLDeleteResponseHandler responseHandler

    void delete(DataFetchingEnvironment environment) {
        withTransaction(false) {
            GormEntity instance = queryInstance(environment)
            deleteInstance(instance)
        }
    }

    protected void deleteInstance(GormEntity instance) {
        instance.delete()
    }

    @Override
    T get(DataFetchingEnvironment environment) {
        boolean success = false
        Exception exception
        try {
            delete(environment)
            success = true
        } catch (Exception e) {
            exception = e
        }

        (T)responseHandler.createResponse(environment, success, exception)
    }
}
